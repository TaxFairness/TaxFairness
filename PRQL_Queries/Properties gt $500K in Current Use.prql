# Properties with appraisals > $50K
# that are in Current Use

prql target:sql.sqlite

# dollars displays a numeric value as dollars with commas
let dollars = d -> s"""printf("$%,d",{d})"""

# pct computes the amount (percent) the new differs from old
let pct = o n -> 100.0*( n - o ) / o

# prtpct prints a floating point number with "%"
let prtpct = v -> s'printf("%1.1f%", {v})'
# let taxrate = 23.96 # 2022
let taxrate = 25.35 # 2023


from sd = ScrapedData
filter (SD_Version == 14)  # use latest 
select {
  adrs=SD_Street_Address,
  appr=SD_Appraisal,
  assm= SD_Assessment,
  taxAppr = appr*taxrate/1000,
  taxAssm = assm*taxrate/1000,
  delta= taxAppr - taxAssm,
  `Tax Savings` = (dollars delta),
  pct = prtpct ( pct taxAssm taxAppr)
}
filter appr > 50_000
filter appr != assm
sort taxAssm / taxAppr

# Total up all the tax savings for Current Use
select {
  `Total Savings to Lyme Residents`=(dollars (sum delta))
}
take 1
