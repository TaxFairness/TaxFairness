# Non-conforming minimum road frontage

# sig_fig - print a value with n decimal places
# inserts comma for thousands/millions 
let sig_fig = v n -> s"printf('%,1.{n}f', {v})"

# prtpct prints a floating point number with "%"
let prtpct = v -> s'printf("%1.1f%", {v})'

# MinRoadFrontage distinguishes between on-state-road and not
# Take a two/three character district, return minimum road frontage
let MinRoadFrontage = dist -> case [
  dist == "RD" => 300,
  dist == "BD" => 200,
  dist == "LCD" => 100,
  dist == "ELD" => 600,
  dist == "SKIWAY" => 600,
  dist == "MFD" => 1000,
  true => 5000,
]

# MinRoadFrontage distinguishes between on-state-road and not
# Take a two/three character district, return minimum road frontage
# MinRoadFrontage90 sets thresholds so that 90% of parcels in Lyme conform
let MinRoadFrontage90 = dist -> case [
  dist == "RD" => 175,
  dist == "BD" => 100,
  dist == "LCD" => 70,
  dist == "ELD" => 125,
  dist == "SKIWAY" => 300,
  dist == "MFD" => 700,
  true => 5000,
]
from sd=CleanScrapedData
join ef=EasementsFrontage (sd.PID == ef.EF_PID)

filter sd.Lot_Size != 0

select {
    sd.PID,
    sd.Street_Address,
    sd.Map, sd.Lot, sd.Unit,
    sd.DistrictName,
    MinFront = (MinRoadFrontage sd.District),
    ef.EF_Frontage,
    noFront = case [
        ef.EF_Frontage == 0 => 1,
        true => 0,
    ],
    nonConf = case [
        ef.EF_Frontage ==0 => 0,
        ef.EF_Frontage > MinFront => 0,
        true => 1,                      # 1 means non-conforming
    ],
    
}
group { sd.DistrictName } (
  aggregate {
    `Current min (ft)` = min MinFront,
    NoFrontage = sum noFront,
    NarrowParcels = sum nonConf,
    TotalParcels = count this,
  }
)
derive PercentNoFrontage = (prtpct NoFrontage * 100.0 / TotalParcels)
derive PercentNonConf = (prtpct NarrowParcels * 100.0 / TotalParcels)

select {
    DistrictName,
    `Current min (ft)`,
    NarrowParcels,
    TotalParcels,
    PercentNonConf,
    NoFrontage,
    PercentNoFrontage,
    }

sort { sd.DistrictName }

# ===== Current conformance =====
# DistrictName	Current min (ft)	NarrowParcels	TotalParcels	PercentNonConf	NoFrontage	PercentNoFrontage
# Commercial	200	0	1	0.0%	0	0.0%
# EastLyme	600	37	54	68.5%	0	0.0%
# LymeCommon/Ctr	100	40	147	27.2%	9	6.1%
# MtnForest	1000	6	37	16.2%	19	51.4%
# Rural-State Road	300	51	178	28.7%	16	9.0%
# Rural-Town Road	300	158	599	26.4%	48	8.0%
# Skiway	600	0	4	0.0%	2	50.0%

# ===== 90% Conforming =====
# DistrictName	Current min (ft)	NarrowParcels	TotalParcels	PercentNonConf	NoFrontage	PercentNoFrontage
# Commercial	100	0	1	0.0%	0	0.0%
# EastLyme	125	6	54	11.1%	0	0.0%
# LymeCommon/Ctr	70	14	147	9.5%	9	6.1%
# MtnForest	700	4	37	10.8%	19	51.4%
# Rural-State Road	175	17	178	9.6%	16	9.0%
# Rural-Town Road	175	67	599	11.2%	48	8.0%
# Skiway	300	0	4	0.0%	2	50.0%