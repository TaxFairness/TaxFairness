# Tax per acre based on zoning district

prql target:sql.sqlite

# dollars displays a numeric value as dollars with commas
let dollars = d -> s"""printf("$%,d",{d})"""

# pct computes the amount (percent) the new differs from old
let pct = o n -> 100.0*( n - o ) / o

# prtpct prints a floating point number with "%"
let prtpct = v -> s"printf('%1.1f%', {v})"

# twodig prints a floating point number with two decimal places
let twodig = v -> s"printf('%1.2f', {v})"

let TaxRate = 25.35 # 2023 tax rate

from ScrapedData
filter (SD_Version == 14)
# filter SD_Appraisal != SD_Assessment
derive District = case [
  SD_Description ~= "CONDO" => "Condo",
  SD_Zoning_District == "" => "Unknown",
  SD_Zoning_District == "RD" => "Rural",
  SD_Zoning_District == "ES" => "Rural",
  SD_Zoning_District == "R" => "Rural",
  SD_Zoning_District == "SFR" => "Rural",
  SD_Zoning_District == "URD" => "Rural",
  SD_Zoning_District == "SD" => "Rural",
  SD_Zoning_District == "BD" => "Commercial",
  SD_Zoning_District == "CD" => "Lyme Common/Center",
  SD_Zoning_District == "LDC" => "Lyme Common/Center",
  SD_Zoning_District == "LCD" => "Lyme Common/Center",
  SD_Zoning_District == "SKIWAY" => "Skiway",
  SD_Zoning_District == "ELD" => "East Lyme",
  SD_Zoning_District == "MFD" => "Mtn/Forest",
  true => SD_Zoning_District
]

# sort (District)

group District (
aggregate {
  Acres= ( (sum SD_Lot_Size)),
  NumParcels = count this,
  Apprl = (sum SD_Appraisal),
  Assmt = (sum SD_Assessment),
  Tax = (sum SD_Assessment) *TaxRate/1000,
}
)
select {
  District,
  Acres,
  NumParcels,
  AverageAcres=Acres/NumParcels,
  Appraisal = (dollars Apprl),
  Assessment = (dollars Assmt),
  TotalTax = (dollars Tax),
  Tax,
  dollperacre = Tax / Acres,
}
sort { - dollperacre }
select {
  District,
  NumParcels,
  `Total Acres` = Acres,
  `Avg. Acre per lot`=(twodig AverageAcres),
  Appraisal,
  Assessment,
  Tax = (dollars Tax),
  DollarsPerAcre = (dollars dollperacre),
}

# District	NumParcels	Total Acres	Avg. Acre per lot	Appraisal	Assessment	Tax	DollarsPerAcre
# Lyme Common/Center	139	430.85	3.10	$70,896,600	$69,724,600	$1,767,518	$4,102
# Commercial	2	52	26.00	$2,728,900	$2,568,700	$65,116	$1,252
# Unknown	85	1822.65	21.44	$44,037,300	$39,110,600	$991,453	$543
# Rural	707	18967.52	26.83	$369,610,750	$301,569,500	$7,644,786	$403
# Skiway	4	619.79	154.95	$7,274,300	$6,649,800	$168,572	$271
# East Lyme	52	2335.21	44.91	$18,388,500	$12,967,000	$328,713	$140
# Mtn/Forest	37	7057.96	190.76	$13,543,300	$3,596,500	$91,171	$12
# Condo	53	0	0.00	$11,546,200	$11,546,200	$292,696	$0