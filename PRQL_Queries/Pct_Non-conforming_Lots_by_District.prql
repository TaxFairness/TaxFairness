# =======================================
# Percentage of lot sizes that don't conform to the Ordinance
# Much trickiness was used to clean data:
# - Fudge the zoning districts to make up for Vision errors
# - Properties in Rural district with addresses on state roads 
#   (Orford, Dorchester, East Thetford, Dartmouth College)
#   get 3 acre minimum
# - Ignore condo's
# - Ignore parcels with lotsize = 0
# - Set "TooSmall" to 1 if < min. lot size
# =======================================

prql target:sql.sqlite

# dollars displays a numeric value as dollars with commas
let dollars = d -> s"""printf("$%,d",{d})"""

# pct computes the amount (percent) the new differs from old
let pct = o n -> 100.0*( n - o ) / o

# prtpct prints a floating point number with "%"
let prtpct = v -> s'printf("%1.1f%", {v})'

# sig_fig - print a value with n decimal places
# inserts comma for thousands/millions 
let sig_fig = v n -> s"printf('%,1.{n}f', {v})"

# isStateRoad == 1 if it's a state road
let isStateRoad = road -> case [
  road ~= "ORFORD " => 1,
  road ~= "DARTMOUTH COLLEGE" => 1,
  road ~= "DORCHESTER" => 1,
  road ~= "EAST THETFORD" => 1,
  true => 0,
]
# Clean up zoning districts
let CleanZoningDistrict = zd -> case [
  zd == "ES" => "RD",
  zd == "R" => "RD",
  zd == "SFR" => "RD",
  zd == "URD" => "RD",
  zd == "LCD" => "LCD",
  zd == "CD" => "LCD",
  zd == "LDC" => "LCD",
  zd == "SD" => "RD",
  zd == "" => "Unknown",
  true => zd,
]
# SubDistrict distinguishes between on-state-road and not
# Also might (but doesn't) distinguish between Lyme Center and Lyme Common
# Produces pleasing printable name
let SubDistrict = dist road -> case [
  dist == "RD" && road == 1 => "Rural-State",
  dist == "RD" && road == 0 => "Rural-Town",
  dist == "BD" => "Commercial",
  dist == "LCD" => "LymeCommon/Ctr",
  dist == "ELD" => "EastLyme",
  dist == "SKIWAY" => "Skiway",
  dist == "MFD" => "MtnForest",
  true => dist,
]
# Return min lot size for a zoning district
let MinLotSize = zd -> case [
  zd == "LymeCommon/Ctr" => 1.0,
  zd == "Commercial" => 2.0,
  zd == "Skiway" => 2.0,
  zd == "Rural-State" => 3.0,
  zd == "Rural-Town" => 5.0,
  zd == "EastLyme" => 15.0,
  zd == "MtnForest" => 50.0,
  true => "?",
]

# ============ begin  query ==============
# Using scraped data version 14 from Vision
from sd=ScrapedData
filter (SD_Version == 14)

derive { StateRoad = isStateRoad sd.SD_Street_Address }

# Clean up districts
derive { District = CleanZoningDistrict sd.SD_Zoning_District }

# ZDistrict distinguishes between on-state-road and not
# Also distinguishes between Lyme Center and Lyme Common
derive { ZDistrict = SubDistrict District StateRoad }

# For each zoning district, add in the minimum lot size
derive { MinLot = MinLotSize ZDistrict }

# "TooSmall" is boolean showing whether the lot is smaller than MinLotSize 
derive TooSmall = case [
    sd.SD_Lot_Size >= MinLot => 0,
    true => 1
  ]
# Ignore condo's and zero-size lots
filter !(sd.SD_Description ~= "CONDO")
filter (sd.SD_Lot_Size != 0)
# Pick the interesting columns
select {
  sd.SD_PID,
  sd.SD_Street_Address,
  StateRoad,
  sd.SD_Zoning_District,
  ZDistrict,
  MinLot,
  sd.SD_Lot_Size,
  TooSmall,
}
# Group by zoning district
group { ZDistrict } (
  aggregate {
    MinLotSz = min MinLot,
    TotalAcres = (sig_fig (sum sd.SD_Lot_Size) 1),
    SmallParcels = sum TooSmall,
    TotalParcels = count this,
  }
)
# Compute the percent of small parcels vs all parcels
derive PctSmallParcels = (prtpct 100.0*SmallParcels/TotalParcels)
# Sort by zoning district
sort { MinLotSz }

# ======== Results from April 2024 ==========
# ZDistrict	MinLotSize	TotalAcres	SmallParcels	TotalParcels	PctSmallParcels
# LymeCommon/Ctr	1.0	430.9	59	139	42.4%
# Commercial	2.0	52.0	0	1	0.0%
# Skiway	2.0	619.8	0	4	0.0%
# Rural-State	3.0	3,567.6	45	134	33.6%
# Rural-Town	5.0	15,399.9	198	572	34.6%
# EastLyme	15.0	2,335.2	28	51	54.9%
# MtnForest	50.0	7,058.0	9	37	24.3%
# Unknown	?	1,822.7	82	82	100.0%